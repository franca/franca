/*******************************************************************************
* Copyright (c) 2018 itemis AG (http://www.itemis.de).
* All rights reserved. This program and the accompanying materials
* are made available under the terms of the Eclipse Public License v1.0
* which accompanies this distribution, and is available at
* http://www.eclipse.org/legal/epl-v10.html
*******************************************************************************/
module org.franca.core.dsl.GenerateFrancaIDL

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*
import org.eclipse.emf.mwe2.ecore.EcoreGenerator

var rootPath = ".."
var projectName = "org.franca.core.dsl"

Workflow {
	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = projectName
				rootPath = rootPath
				runtime = {
					enabled = true
					manifest = {
						merge = false
					}
				}
				runtimeTest = {
					enabled = true
					root = "../../tests/${projectName}.tests.base"
				}
				genericIde = {
					enabled = true
					manifest = {
						merge = false
					}
				}
				eclipsePlugin = {
					enabled = true
					manifest = {
						merge = false
					}
				}
				eclipsePluginTest = {
					enabled = true
					root = "../../tests/${projectName}.tests.base"
				}
				createEclipseMetaData = true
			}
			code = {
				encoding = "UTF-8"
				fileHeader = "/*\n * generated by Xtext \${version}\n */"
				// preferXtendStubs = false
			}
		}
		language = StandardLanguage {
			name = "${projectName}.FrancaIDL"
			
			// the grammar is using a predefined ecore model
			referencedResource = "platform:/resource/org.franca.core/model/franca.genmodel"
			
			fileExtensions = "fidl"

			// use an XtextResourceSet throughout the process, which is able to resolve classpath:/ URIs.
			resourceSet = org.eclipse.xtext.resource.XtextResourceSet:theResourceSet {}

			formatter = {
				generateStub = true
				generateXtendStub = true
			}
		}
	}
}
