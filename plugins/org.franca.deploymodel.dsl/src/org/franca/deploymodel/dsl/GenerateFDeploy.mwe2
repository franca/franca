/*******************************************************************************
 * Copyright (c) 2013 itemis AG (http://www.itemis.de).
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *******************************************************************************/
module org.franca.deploymodel.dsl.GenerateFDeploy

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*
import org.eclipse.emf.mwe2.ecore.EcoreGenerator

var rootPath = ".."
var projectName = "org.franca.deploymodel.dsl"

Workflow {
	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = projectName
				rootPath = rootPath
				runtimeTest = {
					enabled = true
					root = "../../tests/${projectName}.tests.base"
				}
				genericIde = {
					enabled = true
				}
				eclipsePlugin = {
					enabled = true
				}
				eclipsePluginTest = {
					enabled = true
					root = "../../tests/${projectName}.tests.base"
				}
				createEclipseMetaData = true
			}
			code = {
				encoding = "UTF-8"
				fileHeader = "/*\n * generated by Xtext \${version}\n */"
				// preferXtendStubs = false
			}
		}
		language = StandardLanguage {
			name = "${projectName}.FDeploy"
			
			// the grammar is using a predefined ecore model
			referencedResource = "platform:/resource/${projectName}/model/FDeploy.genmodel"
			
			// the grammar needs a dependency to the Franca IDL ecore
			referencedResource = "platform:/resource/org.franca.core/model/franca.genmodel"

			fileExtensions = "fdepl"

			// use an XtextResourceSet throughout the process, which is able to resolve classpath:/ URIs.
			resourceSet = org.eclipse.xtext.resource.XtextResourceSet:theResourceSet {}

//			serializer = {
//				generateStub = false
//			}
		}
	}

	component = EcoreGenerator {
		genModel = "platform:/resource/${projectName}/model/FDeploy.genmodel"
        srcPath = "platform:/resource/${projectName}/src-gen"
        generateEdit = false
        generateEditor = false
	}
}
